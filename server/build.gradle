buildscript {
	ext {
		kotlinVer = '1.1.1'
		springBootVer = '1.5.2.RELEASE'
		propdepsPluginVer = '0.0.7'
		kotlinLoggingVer = '1.4.4'
		websocketApiVer = '1.1'
		apacheHttpClientVer = '4.5.3'
		jsonPathVer = '2.2.0'
		okHttpVer = '3.6.0'
	}

	repositories {
		jcenter()
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
		classpath "org.springframework.build.gradle:propdeps-plugin:$propdepsPluginVer"
	}
}

plugins {
	// gradle dependencyUpdates -Drevision=release
	id 'com.github.ben-manes.versions' version '0.14.0'
	id 'com.palantir.docker' version '0.12.0'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

group = 'app'
version = '0.1-SNAPSHOT'

mainClassName = 'li.barlog.app.AppKt'

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

repositories {
	jcenter()
}

configurations {
	all*.exclude module : 'spring-boot-starter-tomcat'
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
	compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVer"

	compile "io.github.microutils:kotlin-logging:$kotlinLoggingVer"

	compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-undertow'

	compile 'org.springframework.boot:spring-boot-starter-websocket'
	compile "javax.websocket:javax.websocket-api:$websocketApiVer"

	compile 'org.springframework.boot:spring-boot-starter-actuator'

	compile 'org.springframework.boot:spring-boot-starter-security'

	compile 'org.springframework.security.oauth:spring-security-oauth2'
	compile 'org.springframework.security:spring-security-jwt'

	testCompile 'org.springframework.security:spring-security-test'
	testCompile "com.jayway.jsonpath:json-path:$jsonPathVer"
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile "org.apache.httpcomponents:httpclient:$apacheHttpClientVer"
	testCompile "com.squareup.okhttp3:okhttp:$okHttpVer"

	optional 'org.springframework.boot:spring-boot-configuration-processor'
}

task copyUI(type: Copy, dependsOn: ':ui:build') {
	from "${project(':ui').projectDir}/dist"
	into 'build/resources/main/static/ui'
}

jar {
	dependsOn copyUI

	archiveName 'app.jar'
}

bootRepackage {
	it.mustRunAfter clean
}

// https://github.com/palantir/gradle-docker
// gradle docker

docker() {
	dependsOn jar, bootRepackage
	name 'websocket-react-oauth2-app'
	tags 'latest'
	files tasks.jar.outputs
	dockerfile 'src/main/docker/Dockerfile'
	buildArgs([
		PORT     : '8081',
		JAVA_OPTS: '-Xms64m -Xmx128m'
	])
	pull true
}
