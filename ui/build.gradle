plugins {
	// gradle dependencyUpdates -Drevision=release
	id 'com.github.ben-manes.versions' version '0.15.0'
	id 'com.moowork.node' version '1.2.0'
}

apply plugin: 'base'

// https://github.com/srs/gradle-node-plugin
// gradle clean npmBuild

task npmLint(type: NpmTask) {
	dependsOn npm_install
	args = ['run', 'lint']
}

task npmTest(type: NpmTask) {
	dependsOn npmLint
	args = ['run', 'test']
}

task test() {
	dependsOn npmTest
}

task deleteDist(type: Delete) {
	delete 'dist'
}

task deleteNodeModules(type: Delete) {
	delete 'node_modules'
}

clean.dependsOn(deleteDist)
clean.dependsOn(deleteNodeModules)

task npmBuild(type: NpmTask) {
	dependsOn deleteDist, npm_install, test
	args = ['run', 'build']
}

build.dependsOn(npmBuild)

node {
	version = '8.2.1'
	npmVersion = '5.3.0'
	download = true
}
